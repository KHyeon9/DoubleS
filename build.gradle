plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.1'
    id 'io.spring.dependency-management' version '1.1.5'
    id "com.github.node-gradle.node" version "7.0.2"
}

group = 'com.doubles'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
    gradlePluginPortal()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'

    // jpa
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

    // security
    implementation 'org.springframework.boot:spring-boot-starter-security'

    // lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // devtools
    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    // websocket
    implementation 'org.springframework.boot:spring-boot-starter-websocket'

    // redis
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'

    // mysql
    runtimeOnly 'com.mysql:mysql-connector-j'

    // jwt
    implementation 'io.jsonwebtoken:jjwt-api:0.12.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.5'

    // redis LocalDateTime 문제 해결을 위한 JavaTimeModule 추가
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'

    // test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
    useJUnitPlatform()
}

// Spring Boot에서 프론트엔드 빌드를 통합하기 위해 사용하는 Gradle Node Plugin의 설정
apply plugin: 'com.github.node-gradle.node'

node {
    // frontend 위치 경로 지정
    nodeProjectDir = file("${project.projectDir}/frontend")
    // 노드 버전 명시
    version = '20.9.0'
    // 필요 노드 버전 자동 다운로드
    download = true
}
// dist 폴더에 빌드해서 생성하는 task
task npmBuild(dependsOn: npmInstall, type: NpmTask) {
    args = ['run', 'build']
}

// task를 통해 빌드
processResources.dependsOn npmBuild

// 직접 vue 실행시 빌드된 것을 복사하는게 아니라 dist에서 옮겨오므로 수정 
processResources {
    // 중복된 파일을 발견했을 때, 둘 중 하나를 제외하고 빌드를 진행합니다.
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    from("${project.projectDir}/frontend/dist") {
        into "static"
    }
}

// 백엔드 빌드와 프론트엔드 빌드의 통합
// compileJava.dependsOn 'npmBuild'